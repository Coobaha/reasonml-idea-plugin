plugins {
    id 'org.jetbrains.intellij' version "0.4.10"
}

version = '0.81-beta'

patchPluginXml {
    pluginDescription "Add support for ReasonML language. Also add support for OCaml, and Dune."
    changeNotes """
        <ul>
            <li>#88 - GoTo/Declaration not looking for locally-defined symbol</li>
            <li>#83 - Support GoTo/Declaration for function-local symbols</li>
            <li>Try to improve quick navigation doc</li>
            <li>Parsing directives (#if, #else, #end)</li>
            <li>Get inferred type from definition if not found in usage</li>
            <li>Some deduplication of pervasives expressions in free completion contributor</li>
            <li>Work on signature conversion (partial conversion)</li>
            <li>Better dot completion when module is an alias (ex: Belt.Map.String.<caret&gt;)</li>
        </ul>

        <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>

        <p/>
        <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
    """
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        plugins = [
                "java",
                "PsiViewer:192-SNAPSHOT" // always have psi viewer installed in sandbox
        ]
        pluginName 'reasonml-plugin-idea'
        //version '173.4674.60' // https://www.jetbrains.com/intellij-repository/releases
        version '192.6817.14'
        updateSinceUntilBuild = false
    }

    runIde {
        systemProperty "refmtWidth", "120"
        jvmArgs = ['-Dcompiler.process.debug.port=5005']
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project('jps-plugin')
}

apply plugin: 'idea'
idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
    }
    module {
        generatedSourceDirs += file('gen')
    }
}

verifyPlugin {
    pluginDirectory "resources"
}
