plugins {
    id 'org.jetbrains.intellij' version "0.4.10"
}

version = '0.79'

patchPluginXml {
    pluginDescription "Add support for ReasonML language. Also add support for OCaml, and Dune."
    changeNotes """
        <ul>
            <li>#176 - Update OCaml parser</li>
            <li>#170 - Show module impl in structure panel</li>
            <li>Don't annotate interface file</li>
            <li>Remove the perf warning for LineMarker
            <li>Improve module resolution when it's only found in implementation file</li>
            <li>Better dot completion for inner module in impl file</li>
            <li>Add line marker for inner module</li>
        </ul>

        <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>

        <p/>
        <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
    """
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        plugins = [
                //"java",
                "PsiViewer:3.28.93" // always have psi viewer installed in sandbox
        ]
        pluginName 'reasonml-plugin-idea'
        version '173.4674.60' // https://www.jetbrains.com/intellij-repository/releases
        updateSinceUntilBuild = false
    }

    runIde {
        systemProperty "refmtWidth", "120"
        jvmArgs = ['-Dcompiler.process.debug.port=5005']
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project('jps-plugin')
}

apply plugin: 'idea'
idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
    }
    module {
        generatedSourceDirs += file('gen')
    }
}
