plugins {
    id 'org.jetbrains.intellij' version "0.3.12"
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        plugins = [
                "PsiViewer:3.28.93" // always have psi viewer installed in sandbox
        ]
        pluginName 'reasonml-plugin-idea'
        version ideaVersion
        updateSinceUntilBuild = false
    }

    runIde {
        systemProperty "refmtWidth", "120"
        jvmArgs = ['-Dcompiler.process.debug.port=5005']
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project('jps-plugin')
}

apply plugin: 'idea'
idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
    module {
        generatedSourceDirs += file('gen')
    }
}
